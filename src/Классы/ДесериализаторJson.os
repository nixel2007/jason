#Использовать annotations

Перем Лог;

// Рефлектор -
Перем Рефлектор;

// Множество -
Перем КэшИзвестныхТипов;

// Множество -
Перем ПримитивныеТипы;

Функция Десериализовать(Строка, ТипОбъекта) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(Строка);

	ДесериализованныйОбъект = ПрочитатьJSON(ЧтениеJSON, Истина);
	
	Объект = ПреобразоватьОбъектДесериализации(ДесериализованныйОбъект, ТипОбъекта);
	
	Возврат Объект;

КонецФункции

Функция ПреобразоватьОбъектДесериализации(ДесериализованныйОбъект, ТипОбъекта)

	Лог.Информация("Попытка десериализации %1, тип %2", ДесериализованныйОбъект, ТипОбъекта);

	Если ЭтоПримитивныйТип(ТипОбъекта) Тогда
		Возврат ПреобразоватьПримитивныйТип(ДесериализованныйОбъект, ТипОбъекта);
	ИначеЕсли ТипОбъекта = Тип("Массив") Тогда
		Возврат ПреобразоватьМассив(ДесериализованныйОбъект, ТипОбъекта);
	ИначеЕсли ТипОбъекта = Тип("Соответствие") Тогда
		Возврат ПреобразоватьСоответствие(ДесериализованныйОбъект, ТипОбъекта);
	ИначеЕсли ТипОбъекта = Тип("Структура") Тогда
		Возврат ПреобразоватьСоответствие(ДесериализованныйОбъект, ТипОбъекта);
	ИначеЕсли ЭтоПользовательскийТип(ТипОбъекта) Тогда
		Возврат ПреобразоватьКомплексныйТип(ДесериализованныйОбъект, ТипОбъекта);
	Иначе
		Лог.Предупреждение("Неизвестный тип объекта: %1", ТипОбъекта);
	КонецЕсли;	

КонецФункции

Функция ЭтоПримитивныйТип(ТипОбъекта)
	Возврат ПримитивныеТипы.Содержит(ТипОбъекта);
КонецФункции

Функция ЭтоПользовательскийТип(ТипОбъекта)
	Возврат КэшИзвестныхТипов.Содержит(ТипОбъекта);
КонецФункции

Функция ПреобразоватьПримитивныйТип(Объект, ТипОбъекта)
	Возврат Объект;
КонецФункции

Функция ПреобразоватьМассив(Объект, ТипОбъекта)
	
	Результат = Новый Массив;

	Для Каждого Элемент Из Объект Цикл
		ПреобразованныйЭлемент = ПреобразоватьОбъектДесериализации(Элемент, ТипЗнч(Элемент));
		Результат.Добавить(ПреобразованныйЭлемент);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьСоответствие(Объект, ТипОбъекта)

	Результат = Новый(ТипОбъекта);

	Для Каждого КлючИЗначение Из Объект Цикл
		ПреобразованноеЗначение = ПреобразоватьОбъектДесериализации(КлючИЗначение.Значение, ТипЗнч(КлючИЗначение.Значение));
		Результат.Вставить(КлючИЗначение.Ключ, ПреобразованноеЗначение);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьКомплексныйТип(Объект, ТипОбъекта)

	Результат = Новый(ТипОбъекта);
	СвойстваОбъекта = Рефлектор.ПолучитьТаблицуСвойств(ТипОбъекта, Истина);
	СвойстваПоИменамПолей = Новый Соответствие();

	Для Каждого Свойство Из СвойстваОбъекта Цикл
		АннотацияСериализуемое = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Сериализуемое");
		ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, "Значение", Свойство.Имя);
		
		СвойстваПоИменамПолей.Вставить(ИмяСвойства, Свойство);
	КонецЦикла;

	Для Каждого КлючИЗначение Из Объект Цикл
		Лог.Информация("Обработка поля '%1'", КлючИЗначение.Ключ);
		Свойство = СвойстваПоИменамПолей[КлючИЗначение.Ключ];
		Если Свойство = Неопределено Тогда
			// Если поле не найдено, то пропускаем его
			// TODO: Падать, если не висит @JsonIgnore
			Продолжить;
		КонецЕсли;

		ИмяСвойства = Свойство.Имя;
		ЗначениеСвойства = КлючИЗначение.Значение;
		ТипСвойства = ТипЗнч(ЗначениеСвойства);

		АннотацияСериализуемое = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Сериализуемое"); // TODO: В кэш
		Если АннотацияСериализуемое <> Неопределено Тогда
			// TODO: Проверить наличие обязательных полей.
			// TODO: Проверить заполнение не-пустых полей. Подключить nixel2007/validate. Или лучше на уровне обработчика &Валидно?
			ТипСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, "Тип", Строка(ТипСвойства), Истина);
			ТипСвойства = Тип(ТипСвойства);
		КонецЕсли;

		ЗначениеСвойства = ПреобразоватьОбъектДесериализации(ЗначениеСвойства, ТипСвойства);
		Если ТипСвойства <> Неопределено И ТипСвойства <> ТипЗнч(ЗначениеСвойства) Тогда
			Лог.Ошибка("Тип свойства '%1' не соответствует ожидаемому типу '%2'", ТипЗнч(ЗначениеСвойства), ТипСвойства);
			Продолжить;
		КонецЕсли;

		Рефлектор.УстановитьСвойство(Результат, ИмяСвойства, ЗначениеСвойства);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ПриСозданииОбъекта()
	Лог = Логирование.ПолучитьЛог("oscript.lib.jason.ДесериализаторJson");
	Рефлектор = Новый Рефлектор();

	КэшИзвестныхТипов = Новый МножествоСоответствие;
	ИзвестныеТипы = Рефлектор.ИзвестныеТипы(Новый Структура("Пользовательский", Истина));
	Для Каждого ИзвестныйТип Из ИзвестныеТипы Цикл
		КэшИзвестныхТипов.Добавить(ИзвестныйТип.Значение);
	КонецЦикла;

	ПримитивныеТипы = Новый МножествоСоответствие;
	ПримитивныеТипы.Добавить(Тип("Строка"));
	ПримитивныеТипы.Добавить(Тип("Булево"));
	ПримитивныеТипы.Добавить(Тип("Дата"));
	ПримитивныеТипы.Добавить(Тип("Число"));
	ПримитивныеТипы.Добавить(Тип("Null"));
	ПримитивныеТипы.Добавить(Тип("Неопределено"));
КонецПроцедуры

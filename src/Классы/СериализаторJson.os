#Использовать annotations
#Использовать logos

&Лог
Перем Лог;

Перем Рефлектор;

Функция Сериализовать(Объект) Экспорт
	ОбъектСериализации = ПреобразоватьОбъектСериализации(Объект);

	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, ОбъектСериализации);

	Возврат Запись.Закрыть();
КонецФункции

Функция ПреобразоватьОбъектСериализации(Объект)
	ПримитивныеТипы = Новый Массив;
	ПримитивныеТипы.Добавить(Тип("Строка"));
	ПримитивныеТипы.Добавить(Тип("Булево"));
	ПримитивныеТипы.Добавить(Тип("Дата"));
	ПримитивныеТипы.Добавить(Тип("Число"));
	ПримитивныеТипы.Добавить(Тип("Null"));
	ПримитивныеТипы.Добавить(Тип("Неопределено"));


	ТипОбъекта = ТипЗнч(Объект);
	Лог.Информация("Попытка сериализации %1, тип %2", Объект, ТипОбъекта);

	// Массив
	Если ТипОбъекта = Тип("Массив") Тогда
		Результат = Новый Массив;

		Для Каждого Элемент Из Объект Цикл
			ПреобразованныйЭлемент = ПреобразоватьОбъектСериализации(Элемент);
			Результат.Добавить(ПреобразованныйЭлемент);
		КонецЦикла;
	// Структура
	ИначеЕсли ТипОбъекта = Тип("Структура") Тогда
		Результат = Новый Структура;

		Для Каждого Элемент Из Объект Цикл
			ПреобразованноеЗначение = ПреобразоватьОбъектСериализации(Элемент.Значение);
			Результат.Вставить(Элемент.Ключ, ПреобразованноеЗначение);
		КонецЦикла;
	// Примитивные типы
	ИначеЕсли ПримитивныеТипы.Найти(ТипОбъекта) <> Неопределено Тогда
		Результат = Объект;
	// Сценарии?
	Иначе
		СвойстваОбъекта = Рефлектор.ПолучитьТаблицуСвойств(ТипОбъекта);
		Методы = Рефлектор.ПолучитьТаблицуМетодов(ТипОбъекта);
		ЭтоСериализуемое = РаботаСАннотациями.НайтиМетодыСАннотацией(Методы, "Сериализуемое").Количество() > 0;

		Если ЭтоСериализуемое Тогда
			Результат = Новый Структура;

			Для Каждого СвойствоОбъекта Из СвойстваОбъекта Цикл
				Обязательное = Ложь;
				ИмяСвойства = СвойствоОбъекта.Имя;
				JsonСвойство = РаботаСАннотациями.НайтиАннотацию(СвойствоОбъекта.Аннотации, "JsonСвойство");
				Если JsonСвойство <> Неопределено Тогда
					ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(JsonСвойство, "Значение", СвойствоОбъекта.Имя);
					Обязательное = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(JsonСвойство, "Обязательное", Обязательное);
				КонецЕсли;
				
				ЗначениеСвойства = Рефлектор.ПолучитьСвойство(Объект, СвойствоОбъекта.Имя);
				ЗначениеСвойства = ПреобразоватьОбъектСериализации(ЗначениеСвойства);
				Если Обязательное ИЛИ ЗначениеСвойства <> Неопределено Тогда
					Результат.Вставить(ИмяСвойства, ЗначениеСвойства);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Лог.Предупреждение("Неизвестный тип объекта для преобразования: %1", ТипОбъекта);
			Результат = Неопределено;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ПриСозданииОбъекта()
	Рефлектор = Новый Рефлектор();
	Лог = Логирование.ПолучитьЛог("oscript.lib.jason.СериализаторJson");
КонецПроцедуры

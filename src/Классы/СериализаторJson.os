#Использовать annotations
#Использовать logos
#Использовать collectionos

&Лог
Перем Лог;

Перем Рефлектор;
Перем КэшИзвестныхТипов;

Функция Сериализовать(Объект) Экспорт
	ОбъектСериализации = ПреобразоватьОбъектСериализации(Объект);

	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, ОбъектСериализации);

	Возврат Запись.Закрыть();
КонецФункции

Функция ПреобразоватьОбъектСериализации(Объект)
	ПримитивныеТипы = Новый Массив;
	ПримитивныеТипы.Добавить(Тип("Строка"));
	ПримитивныеТипы.Добавить(Тип("Булево"));
	ПримитивныеТипы.Добавить(Тип("Дата"));
	ПримитивныеТипы.Добавить(Тип("Число"));
	ПримитивныеТипы.Добавить(Тип("Null"));
	ПримитивныеТипы.Добавить(Тип("Неопределено"));


	ТипОбъекта = ТипЗнч(Объект);
	Лог.Информация("Попытка сериализации %1, тип %2", Объект, ТипОбъекта);

	// Массив
	Если ТипОбъекта = Тип("Массив") Тогда
		Результат = Новый Массив;

		Для Каждого Элемент Из Объект Цикл
			ПреобразованныйЭлемент = ПреобразоватьОбъектСериализации(Элемент);
			Результат.Добавить(ПреобразованныйЭлемент);
		КонецЦикла;
	// Структура
	ИначеЕсли ТипОбъекта = Тип("Структура") Тогда
		Результат = Новый Структура;

		Для Каждого Элемент Из Объект Цикл
			ПреобразованноеЗначение = ПреобразоватьОбъектСериализации(Элемент.Значение);
			Результат.Вставить(Элемент.Ключ, ПреобразованноеЗначение);
		КонецЦикла;
	// Примитивные типы
	ИначеЕсли ПримитивныеТипы.Найти(ТипОбъекта) <> Неопределено Тогда
		Результат = Объект;
	// Сценарии?
	Иначе
		СвойстваОбъекта = Рефлектор.ПолучитьТаблицуСвойств(ТипОбъекта, Истина);
		
		// Проверяем, является ли тип пользовательским типом (используем кэш)
		ЭтоПользовательскийТип = КэшИзвестныхТипов.Содержит(ТипОбъекта);

		Если ЭтоПользовательскийТип Тогда
			Результат = Новый Структура;

			Для Каждого СвойствоОбъекта Из СвойстваОбъекта Цикл
				// Проверяем, не помечено ли поле как несериализуемое
				Несериализуемое = РаботаСАннотациями.НайтиАннотацию(СвойствоОбъекта.Аннотации, "Несериализуемое");
				Если Несериализуемое <> Неопределено Тогда
					Продолжить; // Пропускаем несериализуемые поля
				КонецЕсли;
				
				Обязательное = Ложь;
				ИмяСвойства = СвойствоОбъекта.Имя;
				СериализуемоеСвойство = РаботаСАннотациями.НайтиАннотацию(СвойствоОбъекта.Аннотации, "Сериализуемое");
				Если СериализуемоеСвойство <> Неопределено Тогда
					ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(СериализуемоеСвойство, "Значение", СвойствоОбъекта.Имя);
					Обязательное = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(СериализуемоеСвойство, "Обязательное", Обязательное);
				КонецЕсли;
				
				ЗначениеСвойства = Рефлектор.ПолучитьСвойство(Объект, СвойствоОбъекта.Имя);
				ЗначениеСвойства = ПреобразоватьОбъектСериализации(ЗначениеСвойства);
				Если Обязательное ИЛИ ЗначениеСвойства <> Неопределено Тогда
					Результат.Вставить(ИмяСвойства, ЗначениеСвойства);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Лог.Предупреждение("Неизвестный тип объекта для преобразования: %1", ТипОбъекта);
			Результат = Неопределено;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ПриСозданииОбъекта()
	Рефлектор = Новый Рефлектор();
	Лог = Логирование.ПолучитьЛог("oscript.lib.jason.СериализаторJson");
	
	// Инициализируем кэш известных пользовательских типов
	КэшИзвестныхТипов = Новый МножествоСоответствие;
	ИзвестныеТипы = Рефлектор.ИзвестныеТипы(Новый Структура("Пользовательский", Истина));
	Для Каждого ИзвестныйТип Из ИзвестныеТипы Цикл
		КэшИзвестныхТипов.Добавить(ИзвестныйТип.Значение);
	КонецЦикла;
КонецПроцедуры

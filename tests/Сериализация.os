#Использовать ".."
#Использовать "."
#Использовать asserts

&Тест
Процедура СериализацияКласса() Экспорт

	// Дано
	ТестовыйКласс = Новый ТестовыйКласс();
	ТестовыйКласс.Число = 1;
	ТестовыйКласс.Строка = "Строка";
	ТестовыйКласс.Булево = Истина;
	ТестовыйКласс.Дата = Дата(2023, 1, 1);
	ТестовыйКласс.ПолеНеопределено = Неопределено;
	ТестовыйКласс.НеобязательноеНеопределено = Неопределено;
	ТестовыйКласс.ОбязательноеНеопределено = Неопределено;
	ТестовыйКласс.ПолеNull = Null;
	
	ТестовыйКласс.Массив = Новый Массив();
	ТестовыйКласс.Массив.Добавить(1);
	ТестовыйКласс.Массив.Добавить(2);

	ТестовыйКласс.Структура = Новый Структура();
	ТестовыйКласс.Структура.Вставить("поле1", 1);
	ТестовыйКласс.Структура.Вставить("поле2", 2);

	ТестовыйКласс.МассивСтруктур = Новый Массив();
	ТестовыйКласс.МассивСтруктур.Добавить(ТестовыйКласс.Структура);
	ТестовыйКласс.МассивСтруктур.Добавить(ТестовыйКласс.Структура); // BSLLS:DuplicatedInsertionIntoCollection-off

	ТестовыйКласс.Соответствие = Новый Структура();
	ТестовыйКласс.Соответствие.Вставить("поле1", 1);
	ТестовыйКласс.Соответствие.Вставить("поле2", 2);

	ВторойТестовыйКласс = Новый ТестовыйКласс();
	ВторойТестовыйКласс.Число = 2;

	ТестовыйКласс.Ссылка = ВторойТестовыйКласс;

	СериализаторJson = Новый СериализаторJson();
	
	// Когда
	Результат = СериализаторJson.Сериализовать(ТестовыйКласс);

	// Тогда
	Ожидаем.Что(Результат).Не_().Равно("");

	ЧтениеJson = Новый ЧтениеJSON();
	ЧтениеJson.УстановитьСтроку(Результат);
	ПрочитанныйОбъект = ПрочитатьJSON(ЧтениеJson);
	ЧтениеJson.Закрыть();

	Ожидаем.Что(ПрочитанныйОбъект).ИмеетТип("Структура");
	Ожидаем.Что(ПрочитанныйОбъект.number).Равно(ТестовыйКласс.Число);
	Ожидаем.Что(ПрочитанныйОбъект.Строка).Равно(ТестовыйКласс.Строка);
	Ожидаем.Что(ПрочитанныйОбъект.Булево).Равно(ТестовыйКласс.Булево);
	Ожидаем.Что(ПрочитанныйОбъект.Дата).Равно(ТестовыйКласс.Дата);
	Ожидаем.Что(ПрочитанныйОбъект.Свойство("ПолеНеопределено")).Равно(Ложь);
	Ожидаем.Что(ПрочитанныйОбъект.Свойство("НеобязательноеНеопределено")).Равно(Ложь);
	Ожидаем.Что(ПрочитанныйОбъект.ОбязательноеНеопределено).Равно(Неопределено);
	Ожидаем.Что(ПрочитанныйОбъект.ПолеNull).Равно(Неопределено);
	
//	Ожидаем.Что(ПрочитанныйОбъект.Массив).Равно(ТестовыйКласс.Массив);
//	Ожидаем.Что(ПрочитанныйОбъект.Структура).Равно(ТестовыйКласс.Структура);
//	Ожидаем.Что(ПрочитанныйОбъект.МассивСтруктур).Равно(ТестовыйКласс.МассивСтруктур);
//	Ожидаем.Что(ПрочитанныйОбъект.Соответствие).Равно(ТестовыйКласс.Соответствие);
//	Ожидаем.Что(ПрочитанныйОбъект.Ссылка).Равно(ТестовыйКласс.Ссылка);

КонецПроцедуры

&Тест
Процедура СериализацияВсехПолейВключаяБезЭкспорт() Экспорт

	// Дано
	// Тестовый класс с полями как с модификатором Экспорт, так и без него
	ТестовыйКласс = Новый ТестовыйКлассСмешанныеПоля();
	ТестовыйКласс.ПолеСЭкспортом = "ЗначениеЭкспорт";
	ТестовыйКласс.УстановитьПолеБезЭкспорта("ЗначениеБезЭкспорт");
	ТестовыйКласс.ПереименованноеПолеСЭкспортом = "ПереименованноеЭкспорт";
	ТестовыйКласс.УстановитьПереименованноеПолеБезЭкспорта("ПереименованноеБезЭкспорт");

	СериализаторJson = Новый СериализаторJson();
	
	// Когда
	Результат = СериализаторJson.Сериализовать(ТестовыйКласс);

	// Тогда
	Ожидаем.Что(Результат).Не_().Равно("");

	ЧтениеJson = Новый ЧтениеJSON();
	ЧтениеJson.УстановитьСтроку(Результат);
	ПрочитанныйОбъект = ПрочитатьJSON(ЧтениеJson);
	ЧтениеJson.Закрыть();

	Ожидаем.Что(ПрочитанныйОбъект).ИмеетТип("Структура");
	
	// Проверяем, что поля с модификатором Экспорт сериализуются
	Ожидаем.Что(ПрочитанныйОбъект.ПолеСЭкспортом).Равно("ЗначениеЭкспорт");
	Ожидаем.Что(ПрочитанныйОбъект.renamed_field).Равно("ПереименованноеЭкспорт");
	
	// Проверяем, что поля без модификатора Экспорт также сериализуются
	Ожидаем.Что(ПрочитанныйОбъект.ПолеБезЭкспорта).Равно("ЗначениеБезЭкспорт");
	Ожидаем.Что(ПрочитанныйОбъект.renamed_field_no_export).Равно("ПереименованноеБезЭкспорт");

КонецПроцедуры

&Тест
Процедура АвтоматическаяСериализацияСНесериализуемыми() Экспорт

	// Дано
	// Тестовый класс с полями разных типов аннотаций
	ТестовыйКласс = Новый ТестовыйКлассАвтоСериализация();
	ТестовыйКласс.АвтоПоле = "АвтоЗначение";
	ТестовыйКласс.ПереименованноеАвтоПоле = "ПереименованноеЗначение";
	ТестовыйКласс.СкрытоеПоле = "СкрытоеЗначение"; 
	ТестовыйКласс.УстановитьВнутреннееПоле("ВнутреннееЗначение");
	ТестовыйКласс.УстановитьСкрытоеВнутреннееПоле("СкрытоеВнутреннееЗначение");

	СериализаторJson = Новый СериализаторJson();
	
	// Когда
	Результат = СериализаторJson.Сериализовать(ТестовыйКласс);

	// Тогда
	Ожидаем.Что(Результат).Не_().Равно("");

	ЧтениеJson = Новый ЧтениеJSON();
	ЧтениеJson.УстановитьСтроку(Результат);
	ПрочитанныйОбъект = ПрочитатьJSON(ЧтениеJson);
	ЧтениеJson.Закрыть();

	Ожидаем.Что(ПрочитанныйОбъект).ИмеетТип("Структура");
	
	// Проверяем, что поле без аннотаций сериализуется автоматически
	Ожидаем.Что(ПрочитанныйОбъект.АвтоПоле).Равно("АвтоЗначение");
	
	// Проверяем, что поле с JsonСвойство сериализуется с переименованием
	Ожидаем.Что(ПрочитанныйОбъект.renamed_auto_field).Равно("ПереименованноеЗначение");
	
	// Проверяем, что поле без экспорта сериализуется
	Ожидаем.Что(ПрочитанныйОбъект.ВнутреннееПоле).Равно("ВнутреннееЗначение");
	
	// Проверяем, что поля с аннотацией &Несериализуемое НЕ сериализуются
	Ожидаем.Что(ПрочитанныйОбъект.Свойство("СкрытоеПоле")).Равно(Ложь);
	Ожидаем.Что(ПрочитанныйОбъект.Свойство("СкрытоеВнутреннееПоле")).Равно(Ложь);

КонецПроцедуры
